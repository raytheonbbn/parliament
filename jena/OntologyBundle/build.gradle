plugins {
	id 'com.gradle.plugin-publish' version '1.2.1'
	id 'java-gradle-plugin'
}

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	implementation "$commonsCsvDependency"
	//implementation "$jenaCmdsDependency"
	implementation "org.apache.jena:jena-cmds:$jena4Version"
	implementation "$sparqlQueryBuilderDependency"
	//implementation project(':jena:SparqlQueryBuilder')

	testImplementation gradleTestKit()
}

version = '0.1.0'

gradlePlugin {
	website = 'https://github.com/raytheonbbn/parliament/tree/jena-update/jena/OntologyBundle'
	vcsUrl = 'https://github.com/raytheonbbn/parliament/tree/jena-update/jena/OntologyBundle'

	plugins {
		ontologyBundle {
			id = 'com.bbn.parliament.ontology-bundle'
			displayName = 'Ontology Bundle'
			description = 'A Gradle plugin that packages (bundles) an OWL ontology into a form that makes it easy to use in a Java-based software project'
			tags.set([ 'ontology', 'OWL', 'RDF', 'Semantic Web' ])
			implementationClass = 'com.bbn.parliament.ontology_bundle.OntologyBundlePlugin'
		}
	}
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

testing {
	suites {
		test {
			useJUnitJupiter("$jupiterVersion")
		}
	}
}

publishing {
	repositories {
		mavenLocal()
	}
}

task xlatePrefix(type: JavaExec) {
	dependsOn compileJava
	mainClass = 'com.bbn.parliament.ontology_bundle.XlatePrefix'
	classpath = sourceSets.main.runtimeClasspath

	//systemProperty 'PROP_NAME', "property-value"

	args = [
		"$projectDir/test-ontology/owl-time.ttl",			// input
		"$projectDir/test-ontology/owl-time-new.ttl",	// output
		'',															// old prefix
		'time',														// new prefix
	]
}

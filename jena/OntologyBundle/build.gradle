plugins {
	id 'com.gradle.plugin-publish' version '1.2.1'
	id 'java-gradle-plugin'
	//id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "$commonsCsvDependency"
	implementation "$jenaCmdsDependency"
	//implementation "$sparqlQueryBuilderDependency"
	implementation project(':jena:SparqlQueryBuilder')

	testImplementation gradleTestKit()
	testImplementation "$jupiterDependency"
}

version = '0.1.0'
sourceCompatibility = '17'
targetCompatibility = '17'

gradlePlugin {
	website = 'https://github.com/SemWebCentral/parliament/tree/jena-update/jena/OntologyBundle'
	vcsUrl = 'https://github.com/SemWebCentral/parliament/tree/jena-update/jena/OntologyBundle'

	plugins {
		ontologyBundle {
			id = 'org.semwebcentral.parliament.ontology-bundle'
			displayName = 'Ontology Bundle'
			description = 'A Gradle plugin that packages (bundles) an OWL ontology into a form that makes it easy to use in a Java-based software project'
			tags.set([ 'ontology', 'OWL' ])
			implementationClass = 'org.semwebcentral.parliament.ontology_bundle.OntologyBundlePlugin'
		}
	}
}

test {
	useJUnitPlatform()
}

publishing {
	repositories {
		mavenLocal()
	}
}

task xlatePrefix(type: JavaExec) {
	dependsOn compileJava
	mainClass = 'org.semwebcentral.parliament.ontology_bundle.XlatePrefix'
	classpath = sourceSets.main.runtimeClasspath

	//systemProperty 'PROP_NAME', "property-value"

	args = [
		"$projectDir/test-ontology/owl-time.ttl",			// input
		"$projectDir/test-ontology/owl-time-new.ttl",	// output
		'',															// old prefix
		'time',														// new prefix
	]
}

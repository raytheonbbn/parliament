plugins {
	id 'java-gradle-plugin'
}

sourceSets {
	functionalTest {
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "$commonsCsvDependency"
	implementation "$jenaCmdsDependency"
	//implementation "$sparqlQueryBuilderDependency"
	implementation project(':jena:SparqlQueryBuilder')

	testImplementation gradleTestKit()
	testImplementation "$jupiterDependency"

	functionalTestImplementation gradleTestKit()
	functionalTestImplementation "$jupiterDependency"
}

sourceCompatibility = '17'
targetCompatibility = '17'

gradlePlugin {
	plugins {
		ontologyBundle {
			id = 'com.bbn.parliament.ontology_bundle.ontologyBundle'
			implementationClass = 'com.bbn.parliament.ontology_bundle.OntologyBundlePlugin'
		}
	}
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

task functionalTest(type: Test) {
	testClassesDirs = sourceSets.functionalTest.output.classesDirs
	classpath = sourceSets.functionalTest.runtimeClasspath
	mustRunAfter(tasks.named('test'))
}

check {
	dependsOn(tasks.functionalTest)
}

tasks.withType(Test).configureEach {
	useJUnitPlatform()
}

task xlatePrefix(type: JavaExec) {
	dependsOn compileJava
	mainClass = 'com.bbn.parliament.ontology_bundle.XlatePrefix'
	classpath = sourceSets.main.runtimeClasspath

	//systemProperty 'SUNRISE_IMAGE_COLLECTION', "$rootDir/local-image-collection"

	args = [
		"$projectDir/test-ontology/owl-time.ttl",	// input
		"$projectDir/test-ontology/owl-time-new.ttl",				// output
		'',																// old prefix
		'time',															// new prefix
	]
}

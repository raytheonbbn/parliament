plugins {
	id 'java-gradle-plugin'
	id 'com.gradle.plugin-publish' version '1.2.1'
	id 'signing'
	//id "com.github.johnrengelman.shadow" version "8.1.1"
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "$commonsCsvDependency"
	implementation "org.apache.jena:jena-cmds:$jena4Version"
	implementation "$sparqlQueryBuilderDependency"

	testImplementation gradleTestKit()
}

version = '1.0.0'

gradlePlugin {
	website = "https://github.com/${githubOrg}/parliament/tree/jena-update/jena/OntologyBundle"
	vcsUrl = "https://github.com/${githubOrg}/parliament/tree/jena-update/jena/OntologyBundle"

	plugins {
		ontologyBundle {
			id = 'com.bbn.parliament.ontology-bundle'
			displayName = 'Ontology Bundle'
			description = 'A Gradle plugin to package (bundle) an OWL ontology in a jar to make it easy to use in Java-based software projects'
			tags.set([ 'ontology', 'OWL', 'RDF', 'Semantic Web' ])
			implementationClass = 'com.bbn.parliament.ontology_bundle.OntologyBundlePlugin'
		}
	}
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

testing {
	suites {
		test {
			useJUnitJupiter("$jupiterVersion")
		}
	}
}

publishing {
	repositories {
		mavenLocal()
	}
}

task xlatePrefix(type: JavaExec) {
	dependsOn compileJava
	mainClass = 'com.bbn.parliament.ontology_bundle.XlatePrefix'
	classpath = sourceSets.main.runtimeClasspath

	//systemProperty 'PROP_NAME', "property-value"

	args = [
		"$projectDir/test-ontology/owl-time.ttl",			// input
		"$projectDir/test-ontology/owl-time-new.ttl",	// output
		'',															// old prefix
		'time',														// new prefix
	]
}

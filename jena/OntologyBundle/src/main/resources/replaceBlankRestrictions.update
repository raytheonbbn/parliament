prefix owl:   <http://www.w3.org/2002/07/owl#>
prefix rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
prefix xsd:   <http://www.w3.org/2001/XMLSchema#>

delete {
	?restrictionBearer ?incomingPredicate ?r .
	?r a owl:Restriction ;
		owl:onProperty ?prop ;
		?rPredicate ?rValue .
} insert {
	?restrictionBearer ?incomingPredicate ?rUri .
	?rUri a owl:Restriction ;
		owl:onProperty ?prop ;
		?rPredicate ?rValue .
} where {
	values ?incomingPredicate {
		rdfs:subClassOf	# the usual case
		rdf:first			# for restrictions in lists
	}
	values ?rPredicate {
		owl:allValuesFrom
		owl:hasValue
		owl:cardinality
		owl:maxCardinality
		owl:minCardinality
		owl:someValuesFrom
	}
	?restrictionBearer ?incomingPredicate ?r .
	?r a owl:Restriction ;
		owl:onProperty ?prop ;
		?rPredicate ?rValue .
	filter ( isBlank(?r) && !isBlank(?rValue) )
	bind ( concat(str(?prop), "\n", str(?rPredicate), "\n", str(?rValue)) as ?rLN )
	bind ( iri(concat("http://bbn.com/tbox/buc/infoexploit/filled-blank#z", sha256(?rLN))) as ?rUri )
}

prefix owl: <http://www.w3.org/2002/07/owl#>

delete {
	?s ?p ?unionOrIntersectionCls .
	?unionOrIntersectionCls a owl:Class ;
		?unionOrIntersectionOf ?clsList .
} insert {
	?s ?p ?newUnionOrIntersectionCls .
	?newUnionOrIntersectionCls a owl:Class ;
		?unionOrIntersectionOf ?clsList .
} where {
	values ?unionOrIntersectionOf {
		owl:unionOf
		owl:intersectionOf
	}

	?s ?p ?unionOrIntersectionCls .
	?unionOrIntersectionCls ?unionOrIntersectionOf ?clsList .
	optional { ?unionOrIntersectionCls a owl:Class . }
	filter ( isBlank(?unionOrIntersectionCls) && !isBlank(?clsList) )
	bind (?_fillNS as ?fillNS)
	bind ( concat(str(?unionOrIntersectionOf), "\n", str(?clsList)) as ?toHash )
	bind ( iri(concat(str(?fillNS), "z", sha256(?toHash))) as ?newUnionOrIntersectionCls )
}

prefix obo:   <http://purl.obolibrary.org/obo/>
prefix owl:   <http://www.w3.org/2002/07/owl#>
prefix provo: <http://www.w3.org/ns/prov#>
prefix rdfs:  <http://www.w3.org/2000/01/rdf-schema#>

# Works against axiom annotations found in BFO, but likely not others.

delete {
	?axiom a owl:Axiom ;
		owl:annotatedSource ?annSrc ;
		owl:annotatedProperty ?annProp ;
		owl:annotatedTarget ?annTgt ;
		obo:IAO_0010000 ?oboAxiomLabel ;
		provo:dm ?provDataModelSection ;
		provo:definition ?provDefinition ;
		rdfs:seeAlso ?seeAlso ;
		rdfs:comment ?comment .
} insert {
	?axiomIri a owl:Axiom ;
		owl:annotatedSource ?annSrc ;
		owl:annotatedProperty ?annProp ;
		owl:annotatedTarget ?annTgt ;
		obo:IAO_0010000 ?oboAxiomLabel ;
		provo:dm ?provDataModelSection ;
		provo:definition ?provDefinition ;
		rdfs:seeAlso ?seeAlso ;
		rdfs:comment ?comment .
} where {
	?axiom a owl:Axiom ;
		owl:annotatedSource ?annSrc ;
		owl:annotatedProperty ?annProp ;
		owl:annotatedTarget ?annTgt .
	optional { ?axiom obo:IAO_0010000 ?oboAxiomLabel . }
	optional { ?axiom provo:dm ?provDataModelSection . }
	optional { ?axiom provo:definition ?provDefinition . }
	optional { ?axiom rdfs:seeAlso ?seeAlso . }
	optional { ?axiom rdfs:comment ?comment . }
	filter ( isblank(?axiom)
		&& !isblank(?annSrc)
		&& !isblank(?annProp)
		&& !isblank(?annTgt)
		&& (!bound(?oboAxiomLabel) || !isblank(?oboAxiomLabel))
		&& (!bound(?provDataModelSection) || !isblank(?provDataModelSection))
		&& (!bound(?provDefinition) || !isblank(?provDefinition))
		&& (!bound(?seeAlso) || !isblank(?seeAlso))
		&& (!bound(?comment) || !isblank(?comment)) )
	bind (?_fillNS as ?fillNS)
	bind ( concat(str(?annSrc), "\n", str(?annProp), "\n", str(?annTgt), "\n",
		if(bound(?oboAxiomLabel), str(?oboAxiomLabel), ""), "\n",
		if(bound(?provDataModelSection), str(?provDataModelSection), ""), "\n",
		if(bound(?provDefinition), str(?provDefinition), ""), "\n",
		if(bound(?seeAlso), str(?seeAlso), ""), "\n",
		if(bound(?comment), str(?comment), "")) as ?toHash )
	bind ( iri(concat(str(?fillNS), "z", sha256(?toHash))) as ?axiomIri )
}

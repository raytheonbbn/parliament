// Copyright (c) 2023 Raytheon BBN Technologies Corp.

plugins {
	id 'java-library'
	id 'maven-publish'
}

version = parliamentVersion

dependencies {
	api "$jenaArqDependency"
	implementation "$httpClientDependency"
	implementation "$slf4jApiDependency"
	testImplementation "$logbackDependency"
}

configurations.all {
	exclude group: 'xml-apis'
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

task sourcesJar(type: Jar) {
	archiveClassifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	archiveClassifier = 'javadoc'
	from javadoc.destinationDir
}

javadoc {
	source = sourceSets.main.allJava
	classpath = configurations.compileClasspath

	options
	{
		//setMemberLevel JavadocMemberLevel.PUBLIC
		//setAuthor true
		addStringOption('Xdoclint:-missing', '-quiet')
	}
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

testing {
	suites {
		test {
			useJUnitJupiter()
		}
	}
}

test {
	systemProperty 'test.data.path', "${depDataDir}"
}

publishing {
	publications {
		ParliamentClient(MavenPublication) {
			from components.java

			artifact sourcesJar {
				archiveClassifier = 'sources'
			}
			artifact javadocJar {
				archiveClassifier = 'javadoc'
			}

			pom {
				name = 'Parliament Client Library'
				description = "A Java library for accessing a running Parliament server programmatically.${System.lineSeparator}Copyright Â© 2023 Raytheon BBN Technologies Corp."
				url = 'https://github.com/SemWebCentral/parliament/tree/main/jena/ParliamentClient'
				organization {
					name = 'Raytheon BBN Technologies'
					url = 'https://bbn.com/'
				}
				developers {
					developer {
						id = 'iemmons'
						name = 'Ian Emmons'
						email = 'iemmons@rtx.com'
					}
				}
			}
		}
	}

	repositories {
		maven {
			url "${artifactory_contextUrl}/bbn-maven-us-only"
			credentials {
				username = "${artifactory_user}"
				password = "${artifactory_password}"
			}
		}
	}
}

// Copyright (c) 2019, 2020, 2023 RTX BBN Technologies Corp.

plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

version = '2.0.1'

dependencies {
	//api "$jenaArqDependency"
	api "org.apache.jena:jena-arq:$jena4Version"
	implementation "$slf4jApiDependency"
	testImplementation "$logbackDependency"
}

configurations.all {
	exclude group: 'xml-apis'
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	withJavadocJar()
	withSourcesJar()
}

testing {
	suites {
		test {
			useJUnitJupiter("$jupiterVersion")
		}
	}
}

publishing {
	publications {
		SparqlQueryBuilder(MavenPublication) {
			from components.java
			pom {
				name = 'SPARQL Query Builder'
				description = "A Java library for manipulating SPARQL queries.${System.lineSeparator}Copyright Â© 2023 RTX BBN Technologies"
				url = "https://github.com/${githubOrg}/parliament/tree/jena-update/jena/SparqlQueryBuilder"
				packaging = 'jar'
				licenses {
					license {
						name = 'The 3-Clause BSD License'
						url = 'https://opensource.org/license/bsd-3-clause/'
					}
				}
				organization {
					name = 'RTX BBN Technologies'
					url = 'https://bbn.com/'
				}
				developers {
					developer {
						id = 'mallen'
						name = 'Matthew Allen'
						email = 'matthew.allen@rtx.com'
					}
					developer {
						id = 'iemmons'
						name = 'Ian Emmons'
						email = 'iemmons@rtx.com'
					}
				}
				scm {
					connection = "scm:git:git@github.com:${githubOrg}/parliament.git"
					developerConnection = "scm:git:git@github.com:${githubOrg}/parliament.git"
					url = "https://github.com/${githubOrg}/parliament"
				}
			}
		}
	}

	repositories {
		maven {
			name = 'SonatypeOssrhRepo'
			url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
			credentials {
				username "$sonatypeOssrhUser"
				password "$sonatypeOssrhPassword"
			}
		}
		//mavenLocal()
	}
}

signing {
	sign publishing.publications.SparqlQueryBuilder
}

// Copyright (c) 2019, 2020 Raytheon BBN Technologies Corp.

plugins {
	id 'java-library'
	id 'eclipse'
	id 'maven-publish'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'com.bbn.parliament'
version = '1.1.4'

repositories {
	mavenCentral()
}

dependencies {
	api 'org.apache.jena:jena-arq:3.14.0'
	implementation 'org.slf4j:slf4j-api:1.7.28'
	implementation 'org.slf4j:jcl-over-slf4j:1.7.28'
	testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
}

tasks.withType(JavaCompile).all {
	options.compilerArgs.add("-Xlint:all")
}

task sourcesJar(type: Jar) {
	archiveClassifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	archiveClassifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

test {
	useJUnitPlatform()
}

publishing {
	publications {
		SparqlQueryBuilder(MavenPublication) {
			from components.java

			artifact sourcesJar {
				classifier = 'sources'
			}
			artifact javadocJar {
				classifier = 'javadoc'
			}

			pom {
				name = 'SPARQL Query Builder'
				description = "A Java library for manipulating SPARQL queries.${System.lineSeparator}Copyright Â© 2019 Raytheon BBN Technologies Corp."
				url = 'https://github.com/SemWebCentral/parliament/tree/main/jena/SparqlQueryBuilder'
				organization {
					name = 'Raytheon BBN Technologies'
					url = 'http://raytheon.com/'
				}
				developers {
					developer {
						id = 'mallen'
						name = 'Matthew Allen'
						email = 'matthew.allen@raytheon.com'
					}
					developer {
						id = 'iemmons'
						name = 'Ian Emmons'
						email = 'iemmons@raytheon.com'
					}
				}
			}
		}
	}

	repositories {
		//mavenLocal()
		maven {
			name = 'BbnMavenUsOnly'
			url = uri('https://artifacts.bbn.com/artifactory/bbn-maven-us-only/')
			credentials(PasswordCredentials)
		}
	}
}

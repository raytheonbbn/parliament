// Copyright (c) 2023 RTX BBN Technologies Corp.

plugins {
	id 'java-library'
	id 'java-test-fixtures'
	id 'maven-publish'
}

version = parliamentVersion

dependencies {
	api "$jenaArqDependency"
	implementation "$slf4jApiDependency"
	implementation project(':Parliament')
	implementation project(':jena:ParliamentClient')
	implementation project(':jena:SparqlQueryBuilderJ3')
	testImplementation "$logbackDependency"
	testFixturesImplementation "$jupiterDependency"
	testFixturesImplementation project(':jena:ParliamentClient')
}

configurations.all {
	exclude group: 'xml-apis'
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

task sourcesJar(type: Jar) {
	archiveClassifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	archiveClassifier = 'javadoc'
	from javadoc.destinationDir
}

javadoc {
	source = sourceSets.main.allJava
	classpath = configurations.compileClasspath

	options
	{
		//setMemberLevel JavadocMemberLevel.PUBLIC
		//setAuthor true
		addStringOption('Xdoclint:-missing', '-quiet')
	}
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

testing {
	suites {
		test {
			useJUnitJupiter("$jupiterVersion")
		}
	}
}

test {
	systemProperty 'java.library.path', "${deployableDir}/bin"
	systemProperty 'test.data.path', "${depDataDir}"
}

clean {
	delete "${projectDir}/kb-data"
}

publishing {
	publications {
		KbGraph(MavenPublication) {
			from components.java

			artifact sourcesJar {
				archiveClassifier = 'sources'
			}
			artifact javadocJar {
				archiveClassifier = 'javadoc'
			}

			pom {
				name = 'Parliament Client Library'
				description = "A Java library that adapts Parliament's native core to Jena's graph and model classes.${System.lineSeparator}Copyright Â© 2023 RTX BBN Technologies Corp."
				url = "https://github.com/${githubOrg}/parliament/tree/main/jena/KbGraph"
				organization {
					name = 'RTX BBN Technologies'
					url = 'https://bbn.com/'
				}
				developers {
					developer {
						id = 'iemmons'
						name = 'Ian Emmons'
						email = 'iemmons@rtx.com'
					}
				}
			}
		}
	}

	repositories {
		maven {
			url "${artifactory_contextUrl}/bbn-maven-us-only"
			credentials {
				username = "${artifactory_user}"
				password = "${artifactory_password}"
			}
		}
	}
}

// Copyright (c) 2023 RTX BBN Technologies Corp.

plugins {
	id 'java-library'
	id 'maven-publish'
}

version = parliamentVersion

dependencies {
	implementation "$berkeleyDbJeDependency"
	implementation "$jenaArqDependency"
	implementation "$slf4jApiDependency"
	implementation project(':jena:ParliamentClient')
	implementation project(':jena:KbGraph')
	testImplementation "$logbackDependency"
	testImplementation(testFixtures(project(":jena:KbGraph")))
}

configurations.all {
	exclude group: 'xml-apis'
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

task sourcesJar(type: Jar) {
	archiveClassifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	archiveClassifier = 'javadoc'
	from javadoc.destinationDir
}

javadoc {
	source = sourceSets.main.allJava
	classpath = configurations.compileClasspath

	options
	{
		//setMemberLevel JavadocMemberLevel.PUBLIC
		//setAuthor true
		addStringOption('Xdoclint:-missing', '-quiet')
	}
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

testing {
	suites {
		test {
			useJUnitJupiter("$jupiterVersion")
		}
	}
}

test {
	systemProperty 'java.library.path', "${deployableDir}/bin"
	systemProperty 'test.data.path', "${depDataDir}"
}

publishing {
	publications {
		TemporalIndex(MavenPublication) {
			from components.java

			artifact sourcesJar {
				archiveClassifier = 'sources'
			}
			artifact javadocJar {
				archiveClassifier = 'javadoc'
			}

			pom {
				name = 'Parliament Client Library'
				description = "A Java library that implements a Parliament index for temporal data.${System.lineSeparator}Copyright Â© 2023 RTX BBN Technologies Corp."
				url = "https://github.com/${githubOrg}/parliament/tree/main/jena/TemporalIndex"
				organization {
					name = 'RTX BBN Technologies'
					url = 'https://bbn.com/'
				}
				developers {
					developer {
						id = 'iemmons'
						name = 'Ian Emmons'
						email = 'iemmons@rtx.com'
					}
				}
			}
		}
	}

	repositories {
		maven {
			url "${artifactory_contextUrl}/bbn-maven-us-only"
			credentials {
				username = "${artifactory_user}"
				password = "${artifactory_password}"
			}
		}
	}
}

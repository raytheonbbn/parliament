plugins {
	id 'org.springframework.boot'
	id 'io.spring.dependency-management'
	id 'java-library'
}

ext {
	genJavaDir = file("$buildDir/generated/sources/java")
	configVocabFile = "${projectDir}/src/main/resources/config-ont.ttl"
	configVocabPackage = 'com.bbn.parliament.server.configuration.vocab'
	configVocabDir = configVocabPackage.replaceAll(/\./, '/')
	configVocabClassNm = 'ConfigOnt'
	configVocabSource = "${genJavaDir}/${configVocabDir}/${configVocabClassNm}.java"
}

configurations {
	schemagen
}

dependencies {
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	schemagen "$jenaCmdsDependency"
	schemagen 'org.apache.logging.log4j:log4j-slf4j-impl:2.20.0'

	implementation project(':Parliament')
	implementation project(':jena:ParliamentClient')
	implementation project(':jena:KbGraph')
	implementation project(':jena:NumericIndex')
	implementation project(':jena:SparqlQueryBuilderJ3')
	implementation project(':jena:SpatialIndex')
	implementation project(':jena:TemporalIndex')

	implementation "$commonsCsvDependency"
	implementation "$commonsIoDependency"
	implementation "$commonsLangDependency"
	implementation "$jenaArqDependency"
	implementation "$slf4jApiDependency"
	implementation 'org.springframework.boot:spring-boot-starter-web'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
}

java {
	sourceSets {
		main {
			java { srcDir genJavaDir }
		}
	}
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	withSourcesJar()
	withJavadocJar()
}

// See https://jena.apache.org/documentation/tools/schemagen.html
task runSchemagen(type: JavaExec) {
	inputs.file(configVocabFile)
	outputs.file(configVocabSource)

	classpath = configurations.schemagen
	mainClass = 'jena.schemagen'
	args = [
		'-i', configVocabFile,
		'-e', 'TURTLE',
		'-o', "${genJavaDir}",
		'--owl',
		'-n', configVocabClassNm,
		'--package', configVocabPackage
	]
}

compileJava.dependsOn runSchemagen

javadoc {
	source = java.sourceSets.main.allJava
	classpath = configurations.compileClasspath
	dependsOn runSchemagen
	options
	{
		//setMemberLevel JavadocMemberLevel.PUBLIC
		//setAuthor true
		addStringOption('Xdoclint:-missing', '-quiet')
	}
}

tasks.getByName('sourcesJar') {
	from java.sourceSets.main.allSource
	dependsOn runSchemagen
}

clean {
	delete "${projectDir}/kb-data"
	delete "${projectDir}/log"
}

testing {
	suites {
		test {
			useJUnitJupiter()
		}
	}
}

test {
	systemProperty 'java.library.path', "${deployableDir}/bin"
	systemProperty 'test.data.path', "${depDataDir}"

	workingDir = buildDir
}

bootRun {
	//def distroDir = targetDir.listFiles().find { it.name =~ /^parliament-[.0-9]+-.*$/ }

	systemProperty 'java.library.path', "${deployableDir}/bin"
}
